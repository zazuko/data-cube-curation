PREFIX "api": <https://rdf-cube-curation.described.at/api#>
PREFIX "hydra": <http://www.w3.org/ns/hydra/core#>

With Class api:Entrypoint {
    Expect Operation api:CreateDataCubeProject
}

With Operation api:CreateDataCubeProject {
    Invoke {
        Header Content-Type "text/csv"

        >>> @test/CreateDataCubeProject/sample-input.csv
    } => {
        Expect Status 201
        Expect Header Location

        Expect Class api:DataCubeProject {
            Expect Link api:DataCubeProject_csvwMetadata {
                Expect Header Content-Type "application/csvm+json"
            }

            Expect Property api:DataCubeProject_column {
                Expect Class hydra:Collection

                Expect Property hydra:member {
                    Expect Class api:DataCubeColumn

                    Expect Property api:DataCubeColumn_unmapped true
                    Expect Property hydra:totalItems 19
                }
            }
        }
    }
}
