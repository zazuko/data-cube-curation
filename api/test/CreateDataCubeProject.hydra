PREFIX api: <https://rdf-cube-curation.described.at/>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>
PREFIX schema: <http://schema.org/>
PREFIX csvw: <http://www.w3.org/ns/csvw#>

With Class api:Entrypoint {
    Expect Property api:dataCubeProjects {
        Expect Operation api:CreateDataCubeProject
    }
}

With Operation api:CreateDataCubeProject {
    Invoke {
        Content-Type "text/csv"

        <<< "bodies/UBD0028.Daten_de.csv"
    } => {
        Expect Status 201
        Expect Header Location

        Expect Type api:DataCubeProject
        Expect Property schema:name

        Expect Link api:DataCubeProject/csvwMetadata {
            Expect Header Content-Type "application/csvm+json"
            Expect Status 200

            Expect Property csvw:tableSchema {
                Expect Property csvw:columns {
                    Expect Property csvw:suppressOutput true
                }

                With Property csvw:columns {
                    //When Property csvw:titles Equals "station_id"

                    Expect Property csvw:propertyUrl "http://environment.data.admin.ch/ubd/28/station_id"
                }
            }
        }

        Expect Property api:DataCubeProject/columns {
            Expect Type hydra:Collection

            Expect Property hydra:totalItems 19
            Expect Property hydra:member {
                Expect Type api:DataCubeColumn

                Expect Property api:DataCubeColumn/mapped false
            }
        }
    }
}
