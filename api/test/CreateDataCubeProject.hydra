PREFIX api: <https://rdf-cube-curation.described.at/api/>
PREFIX dataCube: <https://rdf-cube-curation.described.at/>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>
PREFIX schema: <http://schema.org/>

With Class api:Entrypoint {
    Expect Property api:projects {
        Expect Operation api:CreateProject
    }
}

With Operation api:CreateProject {
    Invoke {
        Content-Type "text/turtle"

        ```
        @prefix schema: <http://schema.org/> .

        <> schema:name "BAFU UBD 28" .
        ```
    } => {
        Expect Status 201
        Expect Header Location [projectUri]

        Follow [projectUri]
    }
}

With Class dataCube:Project {
    Expect Type dataCube:Project
    Expect Property schema:name "BAFU UBD 28"

    Expect Property api:sources {
        Expect Operation api:AddSource
    }

    Expect Link dataCube:factTable
}

With Link api:sources {
    Expect Status 200
}

With Operation api:AddSource {
    // First time like charm
    Invoke {
        Content-Type "text/csv"
        Content-Disposition 'attachment; filename="UBD0028.Daten_de.csv"'

        <<< "bodies/UBD0028.Daten_de.csv"
    } => {
        Expect Status 201
        Expect Header Location [sourceUri]

        Follow [sourceUri]
    }

    // Repeat the same should fail
    Invoke {
        Content-Type "text/csv"
        Content-Disposition 'attachment; filename="UBD0028.Daten_de.csv"'

        <<< "bodies/UBD0028.Daten_de.csv"
    } => {
        Expect Status 409
    }
}

With Class dataCube:Source {
    Expect Type dataCube:Source
    Expect Type dataCube:CsvSource

    Expect Link dataCube:project {
        Expect Link api:sources {
            Expect Property hydra:totalItems 1
        }
    }

    Expect Property api:columns {
        Expect Type hydra:Collection

        Expect Property hydra:totalItems 19
        Expect Property hydra:member {
            Expect Type dataCube:Column

            Expect Property schema:name
        }
    }

    Expect Property schema:name "UBD0028.Daten_de.csv"
}
