PREFIX api: <https://rdf-cube-curation.described.at/api/>
PREFIX dataCube: <https://rdf-cube-curation.described.at/>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>
PREFIX schema: <http://schema.org/>

With Class api:Entrypoint {
    Expect Property api:projects {
        Expect Operation api:CreateProject {
            Invoke {
                Content-Type "text/turtle"

                ```
                @prefix schema: <http://schema.org/> .

                <> schema:name "Project where csv dialect is tested" .
                ```
            } => {
                Expect Status 201
            }
        }
    }
}

With Class dataCube:Project {
    Expect Property api:sources {
        Expect Operation api:AddSource
    }

    With Operation api:DeleteProject {
        Invoke {
            Expect Status 204
        }
    }
}

With Operation api:AddSource {
    Invoke {
        Content-Type "text/csv"
        Content-Disposition 'attachment; filename="semicolon-separated-quoted.csv"'

        <<< "../bodies/semicolon-separated-quoted.csv"
    } => {
        Expect Status 201
        Expect Property dataCube:csvDelimiter ";"
        Expect Property dataCube:csvQuote "\""
    }

    Invoke {
        Content-Type "text/csv"
        Content-Disposition 'attachment; filename="quoted-comma-separated.csv"'

        <<< "../bodies/quoted-comma-separated.csv"
    } => {
        Expect Status 201
        Expect Property dataCube:csvDelimiter ","
        Expect Property dataCube:csvQuote "'"
    }
}
