PREFIX api: <https://rdf-cube-curation.described.at/api/>
PREFIX dataCube: <https://rdf-cube-curation.described.at/>
PREFIX schema: <http://schema.org/>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

With Class api:Entrypoint {
    Expect Property api:projects {
        Expect Operation schema:CreateAction {
            Invoke {
                Content-Type "text/turtle"

                ```
                @prefix schema: <http://schema.org/> .

                <> schema:name "Test that source will get deleted" .
                ```
            } => { }
        }
    }
}

With Class dataCube:Project {
    Expect Property api:sources {
        Expect Property hydra:totalItems 0

        Expect Operation api:AddSource {
            Invoke {
                Content-Type "text/csv"
                Content-Disposition 'attachment; filename="single-column.csv"'

                <<< "../bodies/single-column.csv"
            } => {
                Expect Status 201
            }
        }
    }

    With Link api:sources {
        Expect Property hydra:totalItems 1

        With Property hydra:member {
            Expect Operation api:DeleteSource {
                Invoke {
                    Expect Status 204
                }
            }
        }
    }

    With Link api:sources {
        Expect Property hydra:totalItems 0
    }

    With Operation api:DeleteProject {
        Invoke {
            Expect Status 204
        }
    }
}
