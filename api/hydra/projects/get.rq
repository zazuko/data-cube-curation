BASE <https://rdf-cube-curation.described.at/>

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX resource: <${this.env.BASE_URI}>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>
PREFIX schema: <http://schema.org/>

CONSTRUCT {
    resource:projects ?p ?o ;
        a hydra:Collection ;
        hydra:member ?project ;
        hydra:totalItems ?count .

    ?project
        schema:name ?name .
}
WHERE {
    resource:projects ?p ?o .

    OPTIONAL {
        ?project a <DataCubeProject> .
        OPTIONAL { ?project schema:name ?name . }
    }

    {
        SELECT (COUNT(?p) as ?count) WHERE {
            ?p a <DataCubeProject> .
        }
    }
}
