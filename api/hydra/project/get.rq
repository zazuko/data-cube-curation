PREFIX api: <https://rdf-cube-curation.described.at/api/>
PREFIX dataCube: <https://rdf-cube-curation.described.at/>
PREFIX schema: <http://schema.org/>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>

CONSTRUCT
{
    ?project a ?projectType ;
      schema:name ?name ;
      api:sources ?sources ;
      dataCube:factTable ?factTable .

    ?sources
        a hydra:Collection ;
        hydra:member ?source ;
        hydra:totalItems ?count .

    ?source schema:name ?sourceName
}
FROM <${this.locals.projectId}>
WHERE
{
    BIND (<${this.locals.projectId}> as ?project)
    BIND (<${this.locals.projectId}/sources> as ?sources)
    BIND (<${this.locals.projectId}/fact-table> as ?factTable)

    ?project
        schema:name ?name ;
        a ?projectType .

    OPTIONAL
    {
        ?project dataCube:source ?source .
        ?source schema:name ?sourceName .
    }

    {
        SELECT (COUNT(?source) as ?count)
        {
            BIND (<${this.locals.projectId}> as ?project)
            BIND (<${this.locals.projectId}/sources> as ?sources)

            OPTIONAL { ?project dataCube:source ?source }
        }
    }
}
