BASE <https://rdf-cube-curation.described.at/>
PREFIX schema: <http://schema.org/>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>

CONSTRUCT
{
    ?project a ?projectType ;
      schema:name ?name ;
      <DataCubeProject/sources> ?sources .

    ?sources
        a hydra:Collection ;
        hydra:member ?source ;
        hydra:totalItems ?count .
}
WHERE
{
    BIND (<${this.locals.projectId}> as ?project)
    BIND (<${this.locals.projectId}/sources> as ?sources)

    ?project
        schema:name ?name ;
        a ?projectType .

    OPTIONAL
    {
        ?project <DataCubeProject/source> ?source .
    }

    {
        SELECT (COUNT(?source) as ?count)
        {
            BIND (<${this.locals.projectId}> as ?project)
            BIND (<${this.locals.projectId}/sources> as ?sources)

            OPTIONAL { ?project <DataCubeProject/source> ?source }
        }
    }
}
